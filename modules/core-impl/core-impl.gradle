dependencies {
  api project(':core-api')
  implementation project(':lang-api')
  runtimeOnly project(':lang-impl')

  // logging backend
  implementation(libs.slf4j.api)
  
  testImplementation project(':test-api')
  testImplementation project(':io-api')
  
  testImplementation(libs.junit5.engine)

  testRuntimeOnly project(':io-impl')
  testRuntimeOnly project(':datasets')
  testRuntimeOnly(libs.junit.launcher)
  testRuntimeOnly(libs.bundles.log4j2)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'io.github.muellerj2'
      artifactId 'netroles-networks-engine'
      from components.java
      
      pom {
        name = 'netroles Networks Engine'
        description = 'Networks engine for netroles library'
        url = 'https://github.com/muellerj2/netroles'
        licenses {
          license {
            name = 'GPL-3.0-or-later'
            url = 'https://www.gnu.org/licenses/gpl-3.0-standalone.html'
          }
        }
        developers {
          developer {
            id = 'muellerj2'
            name = 'Julian MÃ¼ller'
            email = 'julian.mueller@gess.ethz.ch'
            organization = "ETH Zurich SN Lab"
            organizationUrl = "https://sn.ethz.ch"
          }
        }
        issueManagement {
          url = 'https://github.com/muellerj2/netroles/issues'
          system = 'github'
        }
        scm {
          connection = 'scm:git:git://github.com/muellerj2/netroles.git'
          developerConnection = 'scm:git:ssh://github.com/muellerj2/netroles.git'
          url = 'https://github.com/muellerj2/netroles/tree/main'
        }
      }
    }
  }
  
  repositories {
    maven {
      name = "buildDirectory"
      url = rootProject.layout.buildDirectory.dir("repository")
    }
  }
}

signing {
  required {
    isToSign && gradle.taskGraph.hasTask("publish")
  }
  sign publishing.publications
}